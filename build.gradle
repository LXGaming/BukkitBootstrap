apply plugin: "java"
apply plugin: "signing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "BukkitBootstrap"
version = "1.0.1"

configurations {
    provided {
        compile.extendsFrom(provided)
    }

    build.dependsOn("signJar")
}

repositories {
    jcenter()
    maven {
        name = "minecraft"
        url = "https://libraries.minecraft.net"
    }
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    provided(fileTree(dir: "./libs/", include: ["spigot-*.jar"]))
    provided("com.google.code.gson:gson:2.8.0")
    provided("com.google.guava:guava:21.0")
    provided("commons-io:commons-io:2.5")
    compile("net.minecraft:launchwrapper:1.12")
    provided("org.apache.logging.log4j:log4j-api:2.8.1")
    provided("org.apache.logging.log4j:log4j-core:2.8.1")
    annotationProcessor("org.apache.logging.log4j:log4j-core:2.8.1")
    provided("org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209")
    compile("org.slf4j:slf4j-simple:1.7.25")
    compile("org.spongepowered:mixin:0.7.5-SNAPSHOT")
}

jar {
    manifest {
        attributes(
            "Main-Class": "io.github.lxgaming.bukkitbootstrap.Main",
            "MixinConfigs": "mixins.bukkitbootstrap.json"
        )
    }

    from {
        (configurations.compile - configurations.provided).findAll({
            it.isDirectory() || it.name.endsWith(".jar")
        }).collect({
            it.isDirectory() ? it : zipTree(it)
        })
    }

    exclude("META-INF/MUMFREY.RSA")
    exclude("META-INF/MUMFREY.SF")
    exclude("Log4j-config.xsd")
    exclude("Log4j-events.dtd")
    exclude("Log4j-events.xsd")
    exclude("log4j2.xml")
}

processResources {
    from("LICENSE")
    rename("LICENSE", "LICENSE-" + archivesBaseName)
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}